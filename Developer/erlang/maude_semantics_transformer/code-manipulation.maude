
*** This module takes in input another maude module, which follows the standard defined in \cite{..},
*** and transforms it in a module that preserves the semantics but also includes the concept of keys.
***

fmod CODE-MANIPULATION is

  protecting META-LEVEL .
  protecting HELPER .
  protecting STRING .

  vars FMOD : FModule .
  vars T1 T2 T3 T4 : Term .
  vars ARG ARGS Q OPNAME OPNAME1 OPNAME2 : Qid .
  var PDL : ParameterDeclList .
  var H : Header .
  var M : Module .
  var IL : ImportList .
  var SS : SortSet .
  var SSDS : SubsortDeclSet .
  var OPDS : OpDeclSet .
  var MAS : MembAxSet .
  var EQ : Equation .
  var EQL : EquationList .
  var EQS EQS' : EquationSet .
  var EQCOND : EqCondition .
  var RLS : RuleSet .
  var ATT : AttrSet .
  var TERMS TERMS' : TermList .
  var SUB : Substitution .
  var VARIABLE : Variable .
  var CONST : Constant .
  var QID : Qid .

  op #transform-mod : Module -> Module .
  op #transform-eqs : EquationList -> EquationSet .
  op #transform-eq-ceq : Equation -> Equation .
  op #transform-eq : Equation -> Equation .
  op #transform-ceq : Equation -> Equation .
  op #transform-op : Term -> Term .
  op #transform-term : Term -> Term .
  op #transform-terms : TermList -> TermList .

  eq #transform-mod(mod H is IL sorts SS . SSDS OPDS MAS EQS RLS endm) =
    mod H is
     IL
     sorts SS .
     SSDS
     OPDS
     MAS
     #transform-eqs( #set-to-list(EQS))
     RLS
    endm .

  ceq #transform-eqs(EQ, EQL)
     = #transform-eq-ceq(EQ) #transform-eqs(EQL)
      if EQL =/= #empty-list [print "transform-eqs = " EQ] .

  eq #transform-eqs(EQ) = #transform-eq-ceq(EQ) [owise] .

  eq #transform-eq-ceq(eq T1 = T2 [ATT] .) = #transform-eq(eq T1 = T2 [ATT] .) .

  eq #transform-eq-ceq(ceq T1 = T2 if EQCOND [ATT] .) = #transform-ceq(ceq T1 = T2 if EQCOND [ATT] .) .

  eq #transform-eq(eq T1 = T2 [ATT] .)
     = eq #transform-term( T1 ) = #transform-term( T2 ) [ATT] . [print "transform-eq"] .

  eq #transform-ceq(ceq T1 = T2 if EQCOND [ATT] .)
     = ceq #transform-term( T1 ) = #transform-term( T2 ) if EQCOND [ATT] . [print "transform-eq"] .

  eq #transform-op( OPNAME[TERMS] )
     = OPNAME[ #transform-terms(TERMS) ] [print "transform-op" ] .

  ceq #transform-terms(TERMS)
     = #transform-term(T1), #transform-terms(TERMS')
    if T1, TERMS' := TERMS /\ TERMS' =/= empty [print "transform-terms"] .

  eq #transform-terms(T1) = #transform-term(T1) [owise] .

  eq #transform-term(OPNAME[TERMS]) = #transform-op(OPNAME[TERMS]) .

  eq #transform-term('E:Entity) = 'E:EntityWithKey [print "yes"] .

  eq #transform-term(CONST) = #conc(#transform-term(getName(CONST)), #conc('., getType(CONST))) [print CONST].

  ceq #transform-term(VARIABLE) = #conc(getName(VARIABLE), ':EntityWithKey)
      if getType(VARIABLE) = 'Entity .

  eq #transform-term(VARIABLE) = VARIABLE [owise] .

  eq #transform-term(QID) = QID .

endfm
