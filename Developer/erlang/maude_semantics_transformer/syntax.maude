fmod SYNTAX is

  protecting QID-LIST .
  protecting META-LEVEL .

  sorts Token Bubble NeTokenList .
  op token : Qid -> Token
       [special
               (id-hook Bubble (1 1)
               op-hook qidSymbol (<Qids> : ~> Qid))] .

  op bubble : QidList -> Bubble
        [special
                (id-hook Bubble (1 -1)
                op-hook qidListSymbol (__ : QidList QidList ~> QidList)
                op-hook qidSymbol (<Qids> : ~> Qid)
                id-hook Exclude (.))] .

  op neTokenList : QidList -> NeTokenList
        [special
                (id-hook Bubble (1 -1)
                op-hook qidListSymbol (__ : QidList QidList ~> QidList) op-hook qidSymbol (<Qids> : ~> Qid)
                id-hook Exclude (->))] .

   sorts Decl DeclList PreModule Input Command .
   subsort Decl < DeclList .

   op including_. : Token -> Decl .
   op protecting_. : Token -> Decl .
   op sort_. : Token -> Decl .
   op subsort_<_. : Token Token -> Decl .
   op op_: ->_. : Token Token -> Decl .
   op op_:_->_. : Token NeTokenList Token -> Decl .
   op op_: ->_[_]. : Token Token AttrSet -> Decl .
   op op_:_->_[_]. : Token NeTokenList Token AttrSet -> Decl .
   op eq_=_. : Bubble Bubble -> Decl .
   op ceq_=_if_. : Bubble Bubble Bubble -> Decl .
   op var_:_. : Token Token -> Decl .
   op vars_:_. : NeTokenList Token -> Decl .

   op fmod_is_endfm : Token DeclList -> PreModule .
   op __ : DeclList DeclList -> DeclList [assoc gather(e E)] .

endfm
